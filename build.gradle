plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}


repositories {
    flatDir { dirs("libraries/") }
    maven { url "https://maven.rizecookey.net/" }
    maven {url "https://api.modrinth.com/maven" }
    maven { url "https://maven.bawnorton.com/releases" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${project.mixin_extras_version}")))
    include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:${project.mixinsquared_version}")))

    modCompileOnlyApi("maven.modrinth:iris:1turazSM") // Iris 1.4.5
    modApi("maven.modrinth:modmenu:aShj7736") // Mod Menu 1.16.23
    modCompileOnlyApi("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") // Fabric API 0.42.0
    modApi("maven.modrinth:screenshot-viewer:U6mIvhIm") // Screenshot Viewer 1.1.1
    modApi("org.dimdev.vanillafix:VanillaFix:2.0.0+beta-29-03-2021") // VanillaFix 2.0.0+beta-29-03-2021
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 8
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = targetJavaVersion
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

loom {
    accessWidenerPath = file("src/main/resources/modfixer.accesswidener")
    customMinecraftManifest = "https://gist.githubusercontent.com/rizecookey/4c6142baaccc3875f9b227fe22f2ace5/raw/c8ed74b19f7a5315813c9d4b199798b692a8f359/1.16_combat-6.json"

    intermediaryUrl = "https://maven.rizecookey.net/net/fabricmc/intermediary/%1\$s/intermediary-%1\$s-v2.jar"
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}